apply plugin: 'java'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'edu.sc.seis.macAppBundle'

import org.gradle.internal.os.OperatingSystem;

buildscript {

    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath group: 'gradle.plugin.edu.sc.seis.gradle', name: 'launch4j', version: '1.4.1'
        classpath group: 'com.badlogicgames.packr', name: 'packr', version: '1.1'
        classpath group: 'gradle.plugin.edu.sc.seis', name: 'macAppBundle', version: '2.1.1'
    }
}

macAppBundle {
    mainClassName = 'ru.flashsafe.Main'
    appName = 'Flashsafe'
    appOutputDir = 'launch4mac'
    icon = "$project.projectDir/resources/AppMacIcon.icns"
    bundleJRE = OperatingSystem.current().isMacOsX()
    //jreHome = '/Users/alex_xpert/Downloads/jre1.8.0_60.jre'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile group: 'ru.flashsafe', name: 'starter', version: '0.1'
}

launch4j {
    mainClassName = "ru.flashsafe.Main"
    outfile = 'Flashsafe.exe'
    icon = "$project.projectDir/resources/AppIcon.ico"
    bundledJrePath = "./jre1.8.0_66"
    version="0.0.0.1"
    copyright="ROFN, LTD"
    companyName="ROFN, LTD"
    description="Infinite Flash Drive"
    productName="Flashsafe"
    opt="-Djava.library.path=./natives/windows"
}

task copyAppLibs(type: Copy) {
    into "$project.buildDir/appLibs"
    from configurations.all
}

task execPackr(type: PackrTask, dependsOn: copyAppLibs) {}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

import com.badlogicgames.packr.Packr
import com.badlogicgames.packr.Packr.Config
import com.badlogicgames.packr.Packr.Platform

class PackrTask extends DefaultTask {

    @TaskAction
    def packr() {
        Config config = new Config();
        config.platform = Platform.windows;
        //config.jdk = "D:/Workspaces/FlashSafe/openjdk-1.7.0-u80-unofficial-windows-i586-image.zip";
        config.executable = "Flashsafe";
        config.jar = "$project.buildDir/appLibs/file-browser-0.1.jar";
        config.mainClass = 'ru.flashsafe.update.Main';
        config.vmArgs = Arrays.asList("-Xmx1G");
        //config.minimizeJre = [ new String("jre/lib/rt/com/sun/corba"), new String("jre/lib/rt/com/sun/jndi") ];
        config.outDir = "$project.buildDir/launch4mac";
        new Packr().pack(config);
    }

}